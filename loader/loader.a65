    org $8000

; BOOT?   = $09
DOSVEC  = $0A
MCBASE  = $D8

ICCHID  = $0340
ICCMD   = $0342
ICBUFA  = $0344
ICBUFL  = $0348
PORTB   = $D301
CIOV    = $E456

START:
; Detect MIDICar
step1:
    jsr detect_MC
    bcs MCNotFound

MCFound:
    sty MCBASE
    stx MCBASE+1

    lda #<MIDICAR_EXIST
    ldy #>MIDICAR_EXIST
    jsr PRINT
    jmp step2

MCNotFound:
    lda #$FE
    sta MCBASE
    lda #$04
    sta MCBASE+1
    lda #$02
    sta $4ff
    lda #<MIDICAR_NOT_FOUND
    ldy #>MIDICAR_NOT_FOUND
    jsr PRINT

; SpartaDOS X detect
step2:
    jsr detect_sdx
    cmp #$ff
    beq step3
    lda #<SDX_FOUND
    ldy #>SDX_FOUND
    jsr PRINT
    jmp exit2DOS

; AXLON Memory extention detect
step3:
    jsr detect_axlon
    cpy #$00
    beq step4
    lda #<AXLON_FOUND
    ldy #>AXLON_FOUND
    jsr PRINT
    jmp exit2DOS

; Determine extended memory size (PORTB based)
step4:
    jsr detect_xms
    lda #$fe
    sta $0100

    lda PORTB
    ora #2
    sta PORTB

    rts
exit2DOS:
    jmp (DOSVEC)

;

    icl 'asm/detect_mc.a65'
    icl 'asm/detect_axlon.a65'
    icl 'asm/detect_sdx.a65'
    icl 'asm/detect_xms.a65'

PRINT:
    ldx #$00
    sta ICBUFA,X
    tya
    sta ICBUFA+1,X
    lda #$ff
    sta ICBUFL,X
    lda #$09
    sta ICCMD,X
    lda ICCHID,x
    bmi ExitPRINT
    jmp CIOV
ExitPRINT:
    rts

//
//

; Strings

MIDICAR_EXIST:
    dta 'Yoppie! MIDICar found :)',$9B
MIDICAR_NOT_FOUND:
    dta 'Ouch! MIDICar not found :(',$9B
AXLON_FOUND:
    dta 'Oh, AXLON extention is not supported :(',$9B
SDX_FOUND:
    dta 'SDX user must wait :P',$9B

    ini START