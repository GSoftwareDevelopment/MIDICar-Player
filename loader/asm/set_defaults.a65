    .local set_defaults

    lda driverBaseAddr+1
    and #$D0
    bne @+

; Default MC Base address - no MC!
    lda #<100
    sta driverBaseAddr
    lda #>100
    sta driverBaseAddr+1

@:
; refresh rate
    lda #1
    sta refreshRate

; color set
    lda #%00000000
    ;       ||
    ;       |+--color set: primary
    ;       +---volume meters: on
    sta screenStatus

; loop mode
    lda #%01
    ;    %00 play once
    ;    %01 repeat one
    ;    %10 repeat all
    ;    %11 shuffle
    ora #%01000000
    ;      1 - determine song length
    sta playerStatus

; colors
    ldy #15
@:
    lda defaultColors,Y
    sta colors_tab,Y
    dey
    bpl @-

; sysex message
    ldy #7
@:
    lda defaultSysExMsg,y
    sta SysEx_MSG-1,Y
    dey
    bpl @-

; clear
    ldy #0
    lda #$9B
@:
    sta $500,y
    dey
    bne @-

; file specification
    ldy #0
@:
    lda defaultFileSpec,Y
    sta outstr,Y
    iny
    cmp #$9b
    bne @-

    rts

defaultFileSpec:
    dta 2,c'D:',$9b

defaultColors:
; primary
; 705 - bottom part of channels
; 706 - selected background
; 707 - selected foreground
; 708 - top part of channels
; 709 - foreground
; 710 - background
; 711 - middle part of channels
; 712 - border color (channels & status background)
;       705,706,707,708,709,710,711,712
    dta $94,$F8,$FF,$24,$98,$00,$E8,$00
; secondary
    dta $04,$0F,$00,$A8,$88,$00,$A8,$00

defaultSysExMsg:
    dta 7, $FF, $f0, $7e, $7f, $09, $01, $f7

    .endl