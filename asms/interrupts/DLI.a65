COLPF1 = $D017  ; (709)
COLPF2 = $D018  ; (710)
COLPF4 = $D01a  ; (712)
WSYNC  = $D40A
NMIRES = $D40f
DLIV   = $200
tmpreg = $6B

  .macro m@PATCH
.ifdef MAIN.@DEFINES.IRQPATCH
    sta NMIRES
.endif
  .endm

  .macro m@setVDLI newVDLI
    lda #<:newVDLI
    ldy #>:newVDLI
    sta DLIV
    sty DLIV+1
  .endm

DLI_setTitleColor:
  m@PATCH
  pha:tya:pha

  lda 710
  ldy #$0f
  sta WSYNC
  sta COLPF2
  sty COLPF1

  lda screenStatus
  and #ss_minMode
  bne noSetCursorColor

  m@setVDLI DLI_setSelectorColor

  pla:tay:pla
  rti

DLI_setSelectorColor:
  m@PATCH
  pha:tya:pha

  ldy 709
  ; sta WSYNC
  sty COLPF1

  lda screenStatus
  and #ss_minMode+ss_isHelp
  bne noSetCursorColor

  m@setVDLI DLI_setCursorColor

  pla:tay:pla
  rti

noSetCursorColor:
  m@setVDLI DLI_setChannelsColor

  pla:tay:pla
  rti


DLI_setCursorColor:
  m@PATCH
  pha:tya:pha

  lda 706
  ldy 707
  sta WSYNC
  sty COLPF1
  sta COLPF2

  m@setVDLI DLI_restoreColor

  pla:tay:pla
  rti

DLI_restoreColor:
  m@PATCH
  pha:tya:pha

  lda 710
  ldy 709
  sta WSYNC
  sty COLPF1
  sta COLPF2

  m@setVDLI DLI_setChannelsColor

  pla:tay:pla
  rti

DLI_setChannelsColor:
  m@PATCH
  pha:tya:pha

  lda 705
  sta WSYNC
  sta COLPF1

  m@setVDLI DLI_setStatusColor

  pla:tay:pla
  rti

DLI_setStatusColor:
  m@PATCH
  pha:tya:pha

  lda 709
  sta WSYNC
  sta COLPF1

  m@setVDLI DLI_setFooterColor

  pla:tay:pla
  rti

DLI_setFooterColor:
  m@PATCH
  pha:tya:pha

  ldy #$0f
  sta WSYNC
  sty COLPF1

  m@setVDLI DLI_setTitleColor

  pla:tay:pla
  rti
