; hlpptr = $D6
  .local

  lda screenStatus
  and #ss_view
  bne dontUpdateUVMeter

updateUVMeter:
  stx regx // txa:pha

  lda #<SCREEN_CHANNELS
  sta scradr
  lda #>SCREEN_CHANNELS
  sta scradr+1

  ldx #0
uvmeterLoop:
  lda MIDFILES.adr.CHNVOLUME,x
  beq dontUpdate

  sub #8
  bpl storeVol
  lda #0
storeVol:
  sta MIDFILES.adr.CHNVOLUME,x

  lsr @
  and #%00111100

  add #<UVMETER_ADDR
  sta hlpptr
  lda #>UVMETER_ADDR
  sta hlpptr+1

  ldy #3          // 2
loop:
  lda hlpptr:$3500,y      // 4*
  sta scradr:$2000,y      // 4*
  dey             // 2
  bpl loop        // 2**

dontUpdate:
  lda scradr        //3
  add #40           //4
  sta scradr        //3
  scc:inc scradr+1  //2**+5

  inx
  cpx #16
  bne uvmeterLoop

  ldx regx:#00 // pla:tax
dontUpdateUVMeter
  .endl